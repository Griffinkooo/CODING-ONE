<!DOCTYPE html>
<html lang="en" dir="ltr">

<head>
  <meta charset="utf-8">
  <title></title>
  <style>
  .div_foot {
    position: absolute;
    height: 50px;
    text-align: center;
    line-height: 50px;
    width: 100%;
    color: #Fb7299;
  </style>
}
</head>

<body>
  <div class="div_foot">Play music on screen click</div>

  <div id="container"></div>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/109/three.min.js">
</script>
  <script src="https://www.doc.gold.ac.uk/~mus02mg/samples.js">
</script>
  
  <script src="https://www.doc.gold.ac.uk/~mus02mg/maxiSynth.js">
</script>
  <script id="vertexShader" type="x-shader/x-vertex">
    void main() { gl_Position = vec4( position, 1.0 ); }
 
</script>
  <script id="fragmentShader" type="x-shader/x-fragment">

  // Based on several ideas and shaders on Shadertoy:
  // 'Plasma Globe' by nimitz (https://www.shadertoy.com/view/XsjXRm) 
  // 'Supernova remnant' by Duke (https://www.shadertoy.com/view/MdKXzc)
  // 'Creation by Silexars' by Danilo Guanabara (https://www.shadertoy.com/view/XsXXDn)

  // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License

  uniform vec2 iResolution;
  uniform float iTime;
  uniform float AverageFrequency;
  uniform vec2 iMouse;
  uniform sampler2D iChannel0;

  #define NUM_RAYS 14. //looks best with around 25 rays
  #define VOLUMETRIC_STEPS 19
  #define MAX_ITER 35
  #define FAR 6.
  #define time iTime*1.1

  #define pi 3.14159265
  #define R(p, a) p=cos(a)*p+sin(a)*vec2(p.y, -p.x)
  
  /////purpule cloud parameters
  const float nudge = 0.9; // random range (size of perpendicular vector)
  float normalizer = 1.0 / sqrt(1.0 + nudge*nudge);	// pythagorean theorem on that perpendicular to
  const int spiralnoisec_num = 3; // cloud shape
  const float initial_iter = 5.0; // cloud density
  const float radius_inv = -5.0; // cloud radius (must be negative num)
  const int raymarch_loop_num  = 30;

  //// iq's noise
  float noise1( in vec3 x )
     {
      vec3 p = floor(x);
      vec3 f = fract(x);
    f = f*f*(3.0-2.0*f);
    vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
    vec2 rg = texture2D( iChannel0, (uv+ 0.5)/256.0, 0.0 ).yx;
   return 1. - 0.82*mix( rg.x, rg.y, f.z );
   }

  
  float fbm(vec3 p)
  {
     return noise1(p*.06125)*.5 + noise1(p*.125)*.25 + noise1(p*.25)*.125 + noise1(p*.4)*.2;
  }
  
  float length2( vec2 p )
  {
    return sqrt( p.x*p.x + p.y*p.y );
  }
  
  float length8( vec2 p )
  {
    p = p*p; p = p*p; p = p*p;
    return pow( p.x + p.y, 1.0/8.0 );
  }
  
  
  float Disk( vec3 p, vec3 t )
  {
      vec2 q = vec2(length2(p.xy)-t.x,p.z*0.5);
      return max(length8(q)-t.y, abs(p.z) - t.z);
  }
 
   // maintain scale
  float SpiralNoiseC(vec3 p)
  {
      float n = 0.0;	// noise amount
      float iter = initial_iter;
      for (int i = 0; i < spiralnoisec_num; i++)
      {
          // add sin and cos scaled inverse with the frequency
          n += -abs(sin(p.y*iter) + cos(p.x*iter)) / iter;	// abs for a ridged look
          // rotate by adding perpendicular and scaling down
          p.xy += vec2(p.y, -p.x) * nudge;
          p.xy *= normalizer;
          // rotate on other axis
          p.xz += vec2(p.z, -p.x) * nudge;
          p.xz *= normalizer;
          // increase the frequency
          iter *= 1.733733;
      }
      return n;
  }
  
  float NebulaNoise(vec3 p)
  {
      float final = Disk(p.xzy,vec3(2.0,1.8,1.25));
      final += fbm(p*90.);
      final += SpiralNoiseC(p.zxy*0.5123+100.0)*3.0;
  
      return final;
  }
  
  float map1(vec3 p) 
  {
    R(p.xz, iMouse.x*0.008*pi+iTime*0.1);
  
    float NebNoise = abs(NebulaNoise(p/0.5)*0.5);
      
    return NebNoise+0.07;
  }
  //--------------------------------------------------------------
 
  // assign color to the media
  vec3 computeColor( float density, float radius )
  {
    // color based on density alone, gives impression of occlusion within the media
    vec3 result = mix( vec3(1.0,0.9,0.8), vec3(0.4,0.15,0.1), density );
    
    // color added to the media
    vec3 colCenter = 7.*vec3(0.8,1.0,1.0);
    vec3 colEdge = 1.5*vec3(0.48,0.53,0.5);
    result *= mix( colCenter, colEdge, min( (radius+.05)/.9, 1.15 ) );
    
    return result;
  }
  
  // Ray/Sphere intersect function to simulate supernova 
  // from this link: https://www.shadertoy.com/view/3lVyRh
  bool RaySphereIntersect(vec3 org, vec3 dir, out float near, out float far)
  {
    float b = dot(dir, org);
    float c = dot(org, org) - 8.;
    float delta = b*b - c;
    if( delta < 0.0) 
      return false;
    float deltasqrt = sqrt(delta);
    near = -b - deltasqrt;
    far = -b + deltasqrt;
    return far > 0.0;
  }
  
  // Applies the filmic curve from John Hable's presentation
  vec3 ToneMapFilmicALU(vec3 _color)
  {
    _color = max(vec3(0), _color - vec3(0.004));
    _color = (_color * (6.2*_color + vec3(0.5))) / (_color * (6.2 * _color + vec3(1.7)) + vec3(0.06));
    return _color;
  }
 
  void mainImage1( out vec4 fragColor, in vec2 fragCoord )
  {  
   
      const float KEY_1 = 49.5/256.0;
      const float KEY_2 = 50.5/256.0;
      const float KEY_3 = 51.5/256.0;
      float key = 0.0;
       key += 0.7*texture2D(iChannel0, vec2(KEY_1,0.25)).x;
      key += 0.7*texture2D(iChannel0, vec2(KEY_2,0.25)).x;
       key += 0.7*texture2D(iChannel0, vec2(KEY_3,0.25)).x;
     
    // ro: ray origin
    // rd: direction of the ray
     vec3 rd = normalize(vec3((fragCoord.xy-0.5*iResolution.xy)/iResolution.y, 1.5));
    vec3 ro = vec3(0.0, 0.1, radius_inv+key*0.6);
 
     
    // ld, td: local, total density 
    // w: weighting factor
    float ld=0., td=0., w=0.;
  
    // t: length of the ray
    // d: distance function
    float d=1., t=0.;
      
      const float h = 0.1;
     
    vec4 sum = vec4(0.0);
     
      float min_dist=0.0, max_dist=0.0;
     
      if(RaySphereIntersect(ro, rd, min_dist, max_dist))
      {
         
      t = min_dist*step(t,min_dist);
      // raymarch loop
     for (int i=0; i< raymarch_loop_num; i++) 
    {
     
      vec3 pos = ro + t*rd;  
      // Loop break conditions.
        if(td>0.9 || d<0.1*t || t>10. || sum.a > 0.99 || t>max_dist) break;
          
          // evaluate distance function
          float d = map1(pos);
   
      // change this string to control density 
      d = max(d,0.0);
          
          // point light calculations
          vec3 ldst = vec3(0.0)-pos;
          float lDist = max(length(ldst), 0.001);
  
          // the color of light 
          vec3 lightColor=vec3(1.0,0.5,1.25);
          
          sum.rgb+=(vec3(0.67,0.75,1.00)/(lDist*lDist*10.)/80.); // star itself
          sum.rgb+=(lightColor/exp(lDist*lDist*lDist*.08)/30.); // bloom
          
      if (d<h) 
      {
        // compute local density 
        ld = h - d; 
        // compute weighting factor 
        w = (1. - td) * ld;
       
        // accumulate density
        td += w + 1./200.;
      
        vec4 col = vec4( computeColor(td,lDist), td );
              
        // emission
        sum += sum.a * vec4(sum.rgb, 0.0) * 0.2;	
              
        // uniform scale density
        col.a *= 0.5;
        // colour by alpha
        col.rgb *= col.a;
        // alpha blend in contribution
        sum = sum + col*(1.0 - sum.a);  
         
      }
      
       td += 1./70.;
     // trying to optimize step size near the camera and near the light source
      t += max(d * 0.1 * max(min(length(ldst),length(ro)),1.0), 0.01);
          
    }
   
      // simple scattering
      sum *= 1. / exp( ld * 0.2 ) * 0.6;    
      sum = clamp( sum, 0.0, 1.0 );
      sum.xyz = sum.xyz*sum.xyz*(3.0-2.0*sum.xyz);//
      
    }
  
      fragColor = vec4(sum.xyz/2.0,1.0);
   
  }

  // Based on 'Creation by Silexars' by Danilo Guanabara (https://www.shadertoy.com/view/XsXXDn)
  void mainImage2(out vec4 fragColor, in vec2 fragCoord ){

    float t = time;
   vec2 r = iResolution.xy;
    vec3 c;
    float l,z=t;
    for(int i=0;i<3;i++) {
      vec2 uv,p=fragCoord.xy/r;
      uv=p;
      p-=.5;
      p.x*=r.x/r.y;
      z+=.07;
      l=length(p);
      uv+=p/l*(sin(z*iTime)+1.)*abs(sin(l*9.-z*2.));
          //l=pow(l,1.3);
      c[i]=0.4/length(abs(mod(uv,1.)-.5));
    }
    fragColor=vec4(c/l,t);
  }
  
  mat2 mm2(in float a){float c = cos(a), s = sin(a);return mat2(c,-s,s,c);}
  float noise( in float x ){return texture2D(iChannel0, vec2(x*.01,1.),0.0).x;}

  float hash( float n ){return fract(sin(n)*43758.5453);}

  float noise(in vec3 p)
  {
    vec3 ip = floor(p);
      vec3 fp = fract(p);
    fp = fp*fp*(3.0-2.0*fp);
    
    vec2 tap = (ip.xy+vec2(37.0,17.0)*ip.z) + fp.xy;
    vec2 rg = texture2D( iChannel0, (tap + 0.5)/256.0, 0.0 ).yx;
    return mix(rg.x, rg.y, fp.z);
  }

  mat3 m3 = mat3( 0.00,  0.80,  0.60,
                -0.80,  0.36, -0.48,
                -0.60, -0.48,  0.64 );

  float flow(in vec3 p, in float t)
  {
    float z=2.;
    float rz = 0.;
    vec3 bp = p;
    for (float i= 1.;i < 5.;i++ )
    {
      p += time*.1;
      rz+= (sin(noise(p+t*0.8)*6.)*0.5+0.5) /z;
      p = mix(bp,p,0.6);
      z *= 2.;
      p *= 2.01;
          p*= m3;
    }
    return rz;  
  }

  //could be improved
  float sins(in float x)
  {
    float rz = 0.;
      float z = 2.;
      for (float i= 0.;i < 3.;i++ )
    {
          rz += abs(fract(x*1.4)-0.5)/z;
          x *= 1.3;
          z *= 1.15;
          x -= time*.65*z;
      }
      return rz;
  }

  float segm( vec3 p, vec3 a, vec3 b)
  {
      vec3 pa = p - a;
    vec3 ba = b - a;
    float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1. );  
    return length( pa - ba*h )*.5;
  }

  vec3 path(in float i, in float d)
  {
      vec3 en = vec3(0.,0.,1.);
      float sns2 = sins(d+i*0.5)*0.22;
      float sns = sins(d+i*.6)*0.21;
      en.xz *= mm2((hash(i*10.569)-.5)*6.2+sns2);
      en.xy *= mm2((hash(i*4.732)-.5)*6.2+sns);
      return en;
  }

  vec2 map(vec3 p, float i)
  {
    float lp = length(p);
      vec3 bg = vec3(0.);   
      vec3 en = path(i,lp);
      
      float ins = smoothstep(0.11,.46,lp);
      float outs = .15+smoothstep(.0,.15,abs(lp-1.));
      p *= ins*outs;
      float id = ins*outs;
      
      float rz = segm(p, bg, en)-0.011;
      return vec2(rz,id);
  }

  float march(in vec3 ro, in vec3 rd, in float startf, in float maxd, in float j)
  {
    float precis = 0.001;
      float h=0.5;
      float d = startf;
      for( int i=0; i<MAX_ITER; i++ )
      {
          if( abs(h)<precis||d>maxd ) break;
          d += h*1.2;
        float res = map(ro+rd*d, j).x;
          h = res;
      }
    return d;
  }

  //volumetric marching
  vec3 vmarch(in vec3 ro, in vec3 rd, in float j, in vec3 orig)
  {   
      vec3 p = ro;
      vec2 r = vec2(0.);
      vec3 sum = vec3(0);
      float w = 0.;
      for( int i=0; i<VOLUMETRIC_STEPS; i++ )
      {
          r = map(p,j);
          p += rd*.03;
          float lp = length(p);
          
          vec3 col = sin(vec3(1.05,2.5,1.52)*3.94+r.y)*.85+0.4;
          col.rgb *= smoothstep(.0,.015,-r.x);
          col *= smoothstep(0.04,.2,abs(lp-1.1));
          col *= smoothstep(0.1,.34,lp);
          sum += abs(col)*5. * (1.2-noise(lp*2.+j*13.+time*5.)*1.1) / (log(distance(p,orig)-2.)+.75);
      }
      return sum;
  }

  //returns both collision dists of unit sphere
  vec2 iSphere2(in vec3 ro, in vec3 rd)
  {
      vec3 oc = ro;
      float b = dot(oc, rd);
      float c = dot(oc,oc) - 1.;
      float h = b*b - c;
      if(h <0.0) return vec2(-1.);
      else return vec2((-b - sqrt(h)), (-b + sqrt(h)));
  }

  void mainImage( out vec4 fragColor, in vec2 fragCoord )
  { 
    vec2 p = fragCoord.xy/iResolution.xy-0.5;
    p.x*=iResolution.x/iResolution.y;
    vec2 um = iMouse.xy / iResolution.xy-.5;
      
    // camera
    vec3 ro = vec3(0.,0.,5.);

    // zoom the globe with average audio frequency
    vec3 rd = normalize(vec3(p*.7, pow(AverageFrequency/150.0, 2.4) * -1.5));

    mat2 mx = mm2(time*.4+um.x*6.);
    mat2 my = mm2(time*0.3+um.y*6.); 
    ro.xz *= mx;rd.xz *= mx;
    ro.xy *= my;rd.xy *= my;
    
    vec3 bro = ro;
    vec3 brd = rd;
  
    vec3 col = vec3(0.0125,0.,0.025);

    for (float j = 1.;j<NUM_RAYS+1.;j++) {
        ro = bro;
        rd = brd;
        mat2 mm = mm2((time*0.1+((j+1.)*5.1))*j*0.25);
        ro.xy *= mm;rd.xy *= mm;
        ro.xz *= mm;rd.xz *= mm;
        float rz = march(ro,rd,2.5,FAR,j);
    if ( rz >= FAR)continue;
      vec3 pos = ro+rz*rd;
      col = max(col,vmarch(pos,rd,j, bro));
    }
    
    ro = bro;
    rd = brd;
    vec2 sph = iSphere2(ro,rd);
    
    vec4 color1,color2;
    if (sph.x > 0.) {
      vec3 pos = ro+rd*sph.x;
      vec3 pos2 = ro+rd*sph.y;
      vec3 rf = reflect( rd, pos );
      vec3 rf2 = reflect( rd, pos2 );
      float nz = (-log(abs(flow(rf*1.2,time)-.01)));
      float nz2 = (-log(abs(flow(rf2*1.2,-time)-.01)));
      col += (0.1*nz*nz* vec3(0.12,0.12,.5) + 0.05*nz2*nz2*vec3(0.55,0.2,.55))*0.8;
     // col += 2.0*(0.1*nz*nz* vec3(0.12,0.12,.5) + 0.15*nz2*nz2*vec3(0.55,0.2,.55))*0.8;

      
     mainImage2(color2, gl_FragCoord.xy);
     mainImage1(color1, gl_FragCoord.xy);
     col.x = col.x +(color2.x)*0.0125+color1.x;
     col.y = col.y +color1.y;
     col.z = col.z +(color2.z)*0.0125+color1.z;
     fragColor = vec4(col*1.3, 1.0);
    }
      
    fragColor = vec4(col*1.3, 1);
  }

  void main() {
    mainImage(gl_FragColor, gl_FragCoord.xy);
  }
 
</script>

  <script>
  var pixel_resolution = 2;  //change the resolution here. 1 is highest

  var avg_freq;
  var container;
  var camera, scene, renderer;
  var uniforms;

  let audio, analyser;

  init();
  animate();

  function init() {
    container = document.getElementById('container');

    // load audio and play
    const audioListener = new THREE.AudioListener();
    audio = new THREE.Audio(audioListener);

    const audioLoader = new THREE.AudioLoader();
    
    audioLoader.load('sunny.mp3', (buffer) => {
      audio.setBuffer(buffer);
      audio.setLoop(true);
      // audio.setVolume(0.5);
      // audio.play();
    });

    // play audio by mouse click
    window.addEventListener("click", function() {
      if (!audio.isPlaying) 
        audio.play();
    })

    // audio analyser: get the average frequency of the sound
    const fftSize = 32;
    analyser = new THREE.AudioAnalyser(audio, fftSize);


    camera = new THREE.Camera();
    camera.position.z = 1;
    scene = new THREE.Scene();
    var geometry = new THREE.PlaneBufferGeometry(2, 2);

    // track mouse move
    var imouse = new THREE.Vector2();
    window.addEventListener("mousemove", function(evt) {
      imouse.x = evt.clientX;
      imouse.y = evt.clientY;
    })


    // load rgb noise texture
    const loader = new THREE.TextureLoader();
    const texture = loader.load('https://threejsfundamentals.org/threejs/resources/images/bayer.png');
    texture.minFilter = THREE.NearestFilter;
    texture.magFilter = THREE.NearestFilter;
    texture.wrapS = THREE.RepeatWrapping;
    texture.wrapT = THREE.RepeatWrapping;

    // uniforms for shader
    uniforms = {
      resolution: {
        value: new THREE.Vector2(window.innerWidth, window.innerHeight)
      },
      iTime: {
        type: "f",
        value: 1.0
      },
      iResolution: {
        type: "v2",
        value: new THREE.Vector2()
      },
      iChannel0: { value: texture },
      AverageFrequency: {
        type: "f",
        value: 150
      },
      iMouse: {
        type: "v2",
        value: imouse
      }};


      var material = new THREE.ShaderMaterial({
        uniforms: uniforms,
        vertexShader: document.getElementById('vertexShader').textContent,
        fragmentShader: document.getElementById('fragmentShader').textContent
      });

      var mesh = new THREE.Mesh(geometry, material);
      scene.add(mesh);

      renderer = new THREE.WebGLRenderer();
      renderer.setPixelRatio(window.devicePixelRatio / pixel_resolution);

      container.appendChild(renderer.domElement);

      onWindowResize();
      window.addEventListener('resize', onWindowResize, false);
    }



    function onWindowResize(event) {
      renderer.setSize(window.innerWidth, window.innerHeight);
      uniforms.iResolution.value.x = renderer.domElement.width;
      uniforms.iResolution.value.y = renderer.domElement.height;
    }

    function animate() {
      requestAnimationFrame(animate);
      render();
    }

    function render() {
      // pass time and average audio frequency to shader
      avg_freq = analyser.getAverageFrequency();
      if (avg_freq < 10) avg_freq = 150;
      uniforms.AverageFrequency.value = avg_freq;
      // console.log(analyser.getAverageFrequency());

      uniforms.iTime.value += 0.05;
      
      renderer.render(scene, camera);      
    }
 
</script>
</body>

</html>